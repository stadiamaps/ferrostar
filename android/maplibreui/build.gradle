plugins {
    alias libs.plugins.androidLibrary
    alias libs.plugins.jetbrainsKotlinAndroid
    alias libs.plugins.ktfmt
    alias libs.plugins.compose.compiler
}

android {
    namespace 'com.stadiamaps.ferrostar.maplibreui'
    compileSdk 34

    defaultConfig {
        minSdk 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // For as long as we support API 25; once we can raise support to 26, all is fine
    coreLibraryDesugaring libs.desugar.jdk.libs

    implementation libs.androidx.ktx
    implementation platform(libs.kotlin.bom)
    implementation libs.androidx.appcompat
    implementation libs.androidx.activity.compose
    implementation libs.androidx.lifecycle.runtime.compose.android
    implementation libs.androidx.lifecycle.viewmodel.compose

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.material3

    api libs.maplibre.compose

    implementation project(':core')
    implementation project(':composeui')

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso
    debugImplementation libs.androidx.compose.ui.test.manifest
}

publishing {
    publications {
        "${project.name}-release"(MavenPublication) {
            groupId = 'com.stadiamaps.ferrostar'
            artifactId = 'maplibreui'
            version = project.version

            afterEvaluate {
                from components.release
            }
        }
    }
}
